############################################################################
# Copyright (c) 2019, QuantStack and Mamba Contributors                    #
#                                                                          #
# Distributed under the terms of the BSD 3-Clause License.                 #
#                                                                          #
# The full license is in the file LICENSE, distributed with this software. #
############################################################################

# libmamba cmake module
# This module sets the following variables in your project::
#
#   libmamba_FOUND - true if libmamba found on the system
#   libmamba_INCLUDE_DIRS - the directory containing libmamba headers
#   libmamba_LIBRARY - the library for dynamic linking
#   libmamba_STATIC_LIBRARY - the library for static linking
#   libmamba_FULL_STATIC_LIBRARY - the library for static linking, incl. static deps

@PACKAGE_INIT@

set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR};${CMAKE_MODULE_PATH}")

@LIBMAMBA_CONFIG_CODE@

include(CMakeFindDependencyMacro)
find_dependency(fmt)
find_dependency(nlohmann_json)
find_dependency(spdlog)
find_dependency(Threads)
find_dependency(tl-expected)

if(NOT (TARGET libmamba OR TARGET libmamba-static OR TARGET libmamba-full-static))
    include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")

    if (TARGET libmamba-static)
        get_target_property(@PROJECT_NAME@_STATIC_LIBRARY libmamba-static LOCATION)
    endif ()
    if (TARGET libmamba-full-static)
        get_target_property(@PROJECT_NAME@_FULL_STATIC_LIBRARY libmamba-full-static LOCATION)
    endif ()

    if (TARGET libmamba)
        get_target_property(@PROJECT_NAME@_INCLUDE_DIR libmamba INTERFACE_INCLUDE_DIRECTORIES)
        get_target_property(@PROJECT_NAME@_LIBRARY libmamba LOCATION)
    elseif (TARGET libmamba-static)
        get_target_property(@PROJECT_NAME@_INCLUDE_DIR libmamba-static INTERFACE_INCLUDE_DIRECTORIES)
        set(@PROJECT_NAME@_LIBRARY ${@PROJECT_NAME@_STATIC_LIBRARY})
    elseif (TARGET libmamba-full-static)
        get_target_property(@PROJECT_NAME@_INCLUDE_DIR libmamba-full-static INTERFACE_INCLUDE_DIRECTORIES)
        set(@PROJECT_NAME@_LIBRARY ${@PROJECT_NAME@_STATIC_LIBRARY})
    endif ()
endif()
